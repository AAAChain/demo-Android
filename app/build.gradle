apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'

def releaseTime() {
  return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def keystorePWD = ''
def keystoreAlias = ''
def keystoreAliasPWD = ''
def keyFile = file('../aaachain')
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystorePWD = properties.getProperty("keystore.password")
keystoreAlias = properties.getProperty("keystore.alias")
keystoreAliasPWD = properties.getProperty("keystore.alias_password")
android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'


  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  defaultConfig {
    applicationId "org.aaa.chain"
    minSdkVersion 19
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }

  signingConfigs {
    release {
      storeFile keyFile
      storePassword keystorePWD
      keyAlias keystoreAlias
      keyPassword keystoreAliasPWD
      v1SigningEnabled true
      v2SigningEnabled true
    }
  }


  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      zipAlignEnabled true

      // 移除无用的resource文件
      shrinkResources true
      //签名
      signingConfig signingConfigs.release

      applicationVariants.all { variant ->
        variant.outputs.all {
          // 输出apk名称为app_v1.0_2015-xx-xx_xxxx.apk
          //            def fileName = "app_v${defaultConfig.versionName}_${releaseTime()}.apk"
          def fileName = "app_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
          outputFileName = new File(fileName)
        }
      }
    }
  }

  sourceSets {
    main {
      jniLibs.srcDirs = ['libs']
    }
  }

  flavorDimensions "data"
  productFlavors {
    aaaChain {
      manifestPlaceholders = [APP_LABLE_VALUE: "@string/app_name", APP_ICON_VALUE: "@mipmap/ic_launcher"]
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation files('libs/hamcrest-core-1.3.jar')
  implementation files('libs/multibase.jar')
  implementation files('libs/multihash.jar')
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support.constraint:constraint-layout:1.1.2'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
  implementation 'com.annimon:stream:1.1.4'
  implementation 'me.tatarka:gradle-retrolambda:3.7.0'
  implementation 'com.android.support:recyclerview-v7:27.1.1'
  implementation 'com.android.support:design:27.1.1'
  implementation 'com.android.support:cardview-v7:27.1.1'
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
  implementation 'com.jaeger.statusbaruitl:library:1.0.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  //  implementation 'org.web3j:core:3.3.1-android'
  implementation group: 'org.web3j', name: 'geth', version: '3.3.1-android'
  implementation "ch.qos.logback:logback-core:1.2.3"
  implementation "ch.qos.logback:logback-classic:1.2.3"
}